sleigh = _{ SOI ~ stmt+ ~ EOI }

stmt = _{ expr }

expr = { preprocess | preprocess_macro | spec }

preprocess = { "@" ~ preprocess_type }
  preprocess_type = _{
    preprocess_include
    | preprocess_define
    | preprocess_undef
    | preprocess_ifdef
    | preprocess_ifndef
    | preprocess_if
    | preprocess_elif
    | preprocess_endif
    | preprocess_else
  }
  preprocess_include = { "include" ~ include_string }
    include_string = _{ "\"" ~ (preprocess_macro | preprocess_include_file)+ ~ "\"" }
      preprocess_include_file = @{ (!"\"" ~ !"$" ~ ANY)+ }
  preprocess_define = { DEFINE_KEY ~ identifier_name ~ (quoted_string | identifier_name) }
  preprocess_undef = { "undef" ~ identifier_name }
  preprocess_ifdef = { "ifdef" ~ identifier_name }
  preprocess_ifndef = { "ifndef" ~ identifier_name }
  preprocess_if = { "if" ~  prep_if_value }
    prep_if_value = _{ boolean_clause ~ (boolop ~ boolean_clause)* }
      boolean_clause = {
        "(" ~ prep_if_value ~ ")"
        | ((defined_expr) | (operand ~ comp ~ operand))
      }
        operand = { (quoted_string | identifier_name) }
        comp = { "==" | "!=" }
        defined_expr = { "defined" ~ "(" ~ identifier_name ~ ")" }
      boolop = { "&&" | "||" | "^^" }
  preprocess_elif = { "elif" ~ prep_if_value }
  preprocess_endif = { "endif" }
  preprocess_else = { "else" }

preprocess_macro = ${ "$(" ~ identifier_name ~ ")" }

spec = {
  endiandef
  | aligndef
  | definition
  | constructorlike
}

definition = { (
    tokendef
    | contextdef
    // | spacedef
    // | varnodedef
    // | bitrangedef
    // | pcodeopdef
    // | valueattach
    // | nameattach
    // | varattach
  )
}

constructorlike = { (
    "macro"
    // constructor
    // | macrodef
    // | withblock
  )
}

endiandef = { DEFINE_KEY ~ "endian" ~ "=" ~ endian_value ~ ";" }
  endian_value = { BIG_KEY | LITTLE_KEY }
aligndef = { DEFINE_KEY ~ "alignment" ~ "=" ~ INTEGER ~ ";" }
tokendef = { tokenprop_expr ~ ";" }
  tokenprop_expr = { tokenprop ~ fielddef_expr* }
    tokenprop = _{
      DEFINE_KEY ~ TOKEN_KEY ~ SYMBOL ~ "(" ~ INTEGER ~ ")"
      | DEFINE_KEY ~ TOKEN_KEY ~ SYMBOL
    }
    fielddef_expr = { fielddef ~ fielddef_type* }
      fielddef = _{ SYMBOL ~ "=" ~ "(" ~ INTEGER ~ "," ~ INTEGER ~ ")" }
      fielddef_type = {
        SIGNED_KEY
        | HEX_KEY
        | DEC_KEY
      }

contextdef = { contextprop ~ ";" }
  contextprop = { contextprop_expr ~ contextfielddef* }
    contextprop_expr = _{ DEFINE_KEY ~ CONTEXT_KEY ~ SYMBOL }
    contextfielddef = { contextfielddef_expr ~ contextfielddef_type* }
      contextfielddef_expr = _{
        (SYMBOL | quoted_string) ~ "=" ~ "(" ~ INTEGER ~ "," ~ INTEGER ~ ")"
        | SYMBOL ~ "=" ~ "(" ~ INTEGER ~ "," ~ INTEGER ~ ")"
      }
      contextfielddef_type = {
        SIGNED_KEY
        | NOFLOW_KEY
        | HEX_KEY
        | DEC_KEY
      }


DEFINE_KEY = _{ "define" }
TOKEN_KEY = _{ "token" }
CONTEXT_KEY = _{ "context" }
SIGNED_KEY = { "signed" }
NOFLOW_KEY = { "noflow" }
HEX_KEY = { "hex" }
DEC_KEY = { "dec" }
BIG_KEY = { "big" }
LITTLE_KEY = { "little" }
identifier_name = @{ (ASCII_ALPHANUMERIC | "_")+ }
quoted_string = _{"\"" ~ string ~ "\""}
string = @{ (!"\"" ~ ANY)* }
SYMBOL = @{ (ASCII_ALPHA | "_" | ".") ~ (ASCII_ALPHANUMERIC | "_" | ".")* }
integer_dec = _{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT }
integer_hex = _{ "0x" ~ ASCII_HEX_DIGIT+ }
integer_bin = _{ "0b" ~ ASCII_BIN_DIGIT+ }
INTEGER = @{ integer_hex | integer_bin | integer_dec }

COMMENT = _{ "#" ~ (!NEWLINE ~ ANY)* }
WHITESPACE = _{ "\r" | " " | "\t" | "\n" }
